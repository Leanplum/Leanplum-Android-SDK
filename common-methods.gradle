// Export methods by turning them into closures
ext {
    COMPILE_SDK_VERSION = 33
    APPCOMPAT_LIBRARY_VERSION = '1.2.0'
    CT_SDK_VERSION = '4.7.2'
    MIN_SDK_VERSION = 19 // CT SDK 4.7.2 has minSDK set as 19
    JAVA_VERSION = JavaVersion.VERSION_1_8

    LEANPLUM_GROUP_ID = 'com.leanplum'
    LEANPLUM_ARTIFACT_ID = 'leanplum'
    LEANPLUM_CORE_ARTIFACT_ID = 'leanplum-core'
    LEANPLUM_PUSH_ARTIFACT_ID = 'leanplum-push'
    LEANPLUM_FCM_ARTIFACT_ID = 'leanplum-fcm'
    LEANPLUM_MIPUSH_ARTIFACT_ID = 'leanplum-mipush'
    LEANPLUM_HMS_ARTIFACT_ID = 'leanplum-hms'
    LEANPLUM_LOCATION_ARTIFACT_ID = 'leanplum-location'
    def sdkVersion = file('../sdk-version.txt').text.replace('\n','')
    LEANPLUM_SDK_VERSION = "$sdkVersion"

    PROGUARD_FILES = 'proguard-rules.pro'
    CONSUMER_PROGUARD_FILES = 'consumer-proguard-rules.pro'

    publishing_task = this.&publishing_task
}

def publishing_task(libraryArtifactId) {

    task androidJavadocs(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        archiveClassifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    afterEvaluate {
        publishing {
            publications {
                aar(MavenPublication) {
                    groupId LEANPLUM_GROUP_ID
                    version LEANPLUM_SDK_VERSION
                    artifactId libraryArtifactId

                    // Apply the component for the release build variant
                    from components.release

                    artifact androidJavadocsJar
                    artifact androidSourcesJar

                    // publish jar
                    artifact("$buildDir/intermediates/aar_main_jar/release/classes.jar")

                    pom {
                        packaging = 'aar'
                        name = libraryArtifactId
                        description = 'The Leanplum SDK messaging platform'
                        url = 'https://github.com/Leanplum/Leanplum-Android-SDK'
                        licenses {
                            license {
                                name = 'The Apache Software License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution = 'repo'
                            }
                        }
                        developers {
                            developer {
                                name = 'Leanplum'
                            }
                        }
                        scm {
                            url = 'https://github.com/Leanplum/Leanplum-Android-SDK'
                        }
                    }
                }
            }

            repositories {
                maven {
                    name = "sonatype"
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                    credentials {
                        username = System.getenv('SONATYPE_OSSRH_USER')
                        password = System.getenv('SONATYPE_OSSRH_PASSWORD')
                    }
                }
            }

        }
    }

    artifactory {
        contextUrl = 'https://repo.leanplum.com'
        publish {
            repository {
                repoKey = '/'

                username = System.getenv('ARTIFACTORY_USER')
                password = System.getenv('ARTIFACTORY_PASSWORD')
            }
            defaults {
                publications('aar')
                publishArtifacts = true

                properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
                publishPom = true
            }
        }
    }

    signing {
        def signingKey = null
        def signingKeyBase64 = System.getenv('SIGNING_KEY_BASE64')
        if (signingKeyBase64 != null) {
            signingKey = new String(Base64.getDecoder().decode((String) signingKeyBase64))
        }
        def signingPassword = System.getenv('SIGNING_PASSWORD')

        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }
}
