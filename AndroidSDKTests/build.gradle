apply plugin: 'com.android.application'
apply from: "../common-methods.gradle"

android {
    compileSdkVersion COMPILE_SDK_VERSION

    defaultConfig {
        applicationId "com.leanplum.tests"
        targetSdkVersion COMPILE_SDK_VERSION
        minSdkVersion MIN_SDK_VERSION
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.all {
            maxParallelForks = 2

            // set heap size for the test JVM(s)
            minHeapSize = "128m"
            maxHeapSize = "2g"

            beforeTest { descriptor ->
                logger.lifecycle("Running test: " + descriptor)
            }


        }
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        abortOnError false
    }
    testOptions.unitTests.includeAndroidResources = true
}

dependencies {
    implementation project(':AndroidSDKCore')
    implementation project(':AndroidSDKPush')
    implementation project(':AndroidSDKLocation')
    implementation project(':AndroidSDKFcm')
    implementation "androidx.appcompat:appcompat:${APPCOMPAT_LIBRARY_VERSION}"
    implementation 'com.google.android.gms:play-services-location:11.6.0'

    // Dependencies used for unit tests.
    testImplementation 'junit:junit:4.12'
    testImplementation('org.robolectric:robolectric:4.1') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation 'org.robolectric:shadows-play-services:3.3.2'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.6'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.6'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.6'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.6.6'
    testImplementation 'org.bouncycastle:bcmail-jdk15on:1.54'
}

task jarTests(type: Jar, dependsOn: "assembleDebugUnitTest") {
    classifier = 'tests'
    from "$buildDir/intermediates/classes/test/debug"
}
configurations {
    unitTestArtifact
}
artifacts {
    unitTestArtifact jarTests
}
