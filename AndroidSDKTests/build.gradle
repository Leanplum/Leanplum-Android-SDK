apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "../common-methods.gradle"
apply plugin: 'com.facebook.testing.screenshot'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.facebook.testing.screenshot:plugin:0.14.0'
    }
}

android {
    compileSdkVersion COMPILE_SDK_VERSION
    namespace "com.leanplum.tests"

    defaultConfig {
        applicationId "com.leanplum.tests"
        targetSdkVersion COMPILE_SDK_VERSION
        minSdkVersion MIN_SDK_VERSION
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.leanplum.ScreenshotTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }
    kotlinOptions {
        jvmTarget = JAVA_VERSION
    }
    testOptions {
        unitTests.all {
            maxParallelForks = 2

            // set heap size for the test JVM(s)
            minHeapSize = "128m"
            maxHeapSize = "2g"

            beforeTest { descriptor ->
                logger.lifecycle("Running test: " + descriptor)
            }

        }
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            systemProperty "javax.net.ssl.trustStoreType", "JKS"
            systemProperty "robolectric.dependency.repo.id", "central"
            systemProperty "robolectric.dependency.repo.url", "https://repo1.maven.org/maven2"
        }
    }
    lint {
        abortOnError false
    }
    testOptions.unitTests.includeAndroidResources = true
}

dependencies {
    implementation project(':AndroidSDKCore')
    implementation project(':AndroidSDKPush')
    implementation project(':AndroidSDKLocation')
    implementation project(':AndroidSDKFcm')
    implementation "androidx.appcompat:appcompat:${APPCOMPAT_LIBRARY_VERSION}"
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // Dependencies used for unit tests.
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.12.2'
    testImplementation 'commons-io:commons-io:2.16.1'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.5'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.5'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.6.5'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.6.5'
    testImplementation 'org.bouncycastle:bcmail-jdk18on:1.71.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:1.4"
    implementation 'com.facebook.testing.screenshot:core:0.14.0'
}

task jarTests(type: Jar, dependsOn: "assembleDebugUnitTest") {
    archiveClassifier = 'tests'
    from "$buildDir/intermediates/classes/test/debug"
}
configurations {
    unitTestArtifact
}
artifacts {
    unitTestArtifact jarTests
}

tasks.withType(Test) {
    jvmArgs '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED',
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/java.net=ALL-UNNAMED',
            '--add-opens=java.base/java.util=ALL-UNNAMED',
            '--add-opens=java.base/java.text=ALL-UNNAMED',
            '--add-opens=java.desktop/java.awt.font=ALL-UNNAMED',
            '--add-opens=java.base/java.security=ALL-UNNAMED',
            '--add-opens=java.base/java.io=ALL-UNNAMED',
            '--add-opens=java.base/sun.security.jca=ALL-UNNAMED',
            '--add-opens=java.base/sun.security.util=ALL-UNNAMED',
            '--add-opens=java.base/jdk.internal.util=ALL-UNNAMED',
            '--add-opens=java.base/javax.net.ssl=ALL-UNNAMED',
            '--add-opens=java.base/java.util.regex=ALL-UNNAMED',
            '--add-opens=java.base/java.util.concurrent=ALL-UNNAMED',
            '--add-opens=java.base/sun.util.calendar=ALL-UNNAMED',
            '--add-opens=java.base/java.util.zip=ALL-UNNAMED',
            '--add-opens=java.base/java.nio.charset=ALL-UNNAMED'

}
