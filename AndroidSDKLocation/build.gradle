plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'maven-publish'
    id 'signing'
}
apply from: "../common-methods.gradle"

android {
    namespace 'com.leanplum.location'
    compileSdkVersion COMPILE_SDK_VERSION
    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        defaultConfig {
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }
}

dependencies {
    api project(':AndroidSDKCore')

    // Provided dependencies are optional dependencies and will not show up in pom file.
    compileOnly('com.google.android.gms:play-services-location:[10.0.0,)') {
        exclude module: 'support-v4'
    }
}
task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("./javadoc/")
    failOnError false
}

publishing_task(LEANPLUM_LOCATION_ARTIFACT_ID)
